- 1 概述
- 2 初始化GDT
    - 2.1 动态建立描述符
    - 2.2 GDT的定位
    - 2.3 加载GDTR
- 3 初始化IDT
    - 3.1 动态设置IDT
    - 3.2 加载IDTR
- 4 切换到保护模式

# 1. 概述

在进入保护模式之前，必须建立一个完整的保护模式运行环境，下面是必须建立的。
- GDT
- IDT
- TSS段

在打开CR0.PE标志前，GDT必须被建立，IDT和TSS允许进入保护模式后才建立。下列环境是可选的。
- Paging机制
- LDT
- 处理器的TSS任务切换机制

在legacy模式的保护模式里paging可以被关闭，虚拟地址直接变成物理地址。
LDT和TSS任务切换机制被使用于多任务环境里。

# 2. 初始化GDT

下面利用nasm编译器的dq伪指令直接建立段描述符。
```asm
GDT:
    ; null_desc          dq 0                    ; NULL Descriptor
    
    code16_desc         dq  0x0000fa000000ffff  ; DPL=0
    data16_desc         dq  0x000092000000ffff  ; DPL=0
    kernel_code32_desc  dq  0x00cf9a000000ffff  ; non-conforming, DPL=1, P=1
    kernel_data32_desc  dq  0x00cf92000000ffff  ; DPL=0, P=1, writebale, expand-up
    user_code32_desc    dq  0x00cffa000000ffff  ; non-conforming, DPL=3, P=1
    user_data32_desc    dq  0x00cff2000000ffff  ; DPL=3, P=1, writebale, expand-up
    
    tss32_desc              dq  0
    call_gate_desc          dq  0
    conforming_code32_desc  dq  0x00cf9e000000ffff  ; conforming, DPL=0, P=1
    tss_test_desc           dq  0
    times 10                dq  0
    GDT_END:
```
nasm编译后这个GDT直接生成在输出文件里，这是最简单的形式。或者定义一个宏来生成描述符的值。
```asm
    ; args: base, limit, attribute
    %macro MAKE_SEGMENT_DESCRIPTOR  3
        dd (%2 & 0x0000ffff) | ((%1 & 0x0000ffff << 16))
        dd ((%1 & 0x00ff0000 >> 16) | (%2 & 0x000f0000) | (%3 << 8) | (%1 & 0xff000000))
    $endmacro
```
这个宏MAKE\_SEGMENT\_DESCRIPTOR有三个参数：base，limit和attribute，那么可以这样使用来定义一个Data段描述符。
```asm
    MAKE_SEGMENT_DESCRIPTOR     0, 0xFFFFF, 0xC092      ; Data, DPL=0, G=D=P=S=1
```
这些是属于静态生成方式，直接写入执行文件里。

## 2.1 动态建立描述符

编写一个函数用来在运行时建立或修改段描述符。

```asm
read_gdt_descriptor()
input:
    esi: vector
output:
    edx:eax descriptor

__read_gdt_descriptor:
    sgdt [__gdt_pointer]
    mov eax, [__gdt_pointer + 2]
    and esi, 0FFF8h
    mov edx, [eax + esi + 4]
    mov eax, [eax + esi]
    ret
    
write_gdt_descriptor()
input:
    esi:vector
    edx:eax descriptor

__write_gdt_descriptor:
    sgdt [__gdt_pointer]
    mov edi, [__gdt_pointer + 2]
    and esi, 0FFF8h
    mov [edi + esi], eax
    mov [edi + esi + 4], edx
    ret
```
这两个函数中，read\_gdt\_descriptor用来读GDT里的描述符，write\_gdt\_descriptor用来写GDT描述符，现在可以在运行时往GDT里读写描述符了。

## 2.2 GDT的定位

上面的运行时读写时已经建立好GDT的基础上进行读写，如果需要在运行时将GDT定位在某个位置的话：
```asm
move_gdt()
input:
    esi: address
    
__move_gdt:
    push ecx
    push esi            ; GDT base
    mov edi, esi
    sgdt [__gdt_pointer]
    mov esi, [__gdt_pointer + 2]
    movzx ecx, WORD [__gdt_pointer]
    push cx             ; GDT limit
    rep movsb
    lgdt [esp]
    add esp, 6
    pop ecx
    ret
```
这个move\_gdt()函数将GDT复制到目标地址后，重新加载GDTR，实现在运行时重新定位GDT。最后一种形式是，直接在某个位置上建立描述符表，然后在加载到GDTR里，不过现实中似乎没有这个必要，使用move\_gdt函数在系统初始化完成前重新定位这已经足够了。

重定位的目的以安全因素为主，将GDT定位在受系统保护的区域，免受干扰。同时也是为了合理管理内存区域。

## 2.3 加载GDTR

初始化GDT后，使用ldgt指令加载到GDTR里，就完成了GDT的设置。
```asm
    lgdt [GDT_POINTER]      ; load GDT to GDTR
```
在这里[GDT_POINTER]是GDT的伪指针值：这个地址的低16位是limit值，接下来的32位是GDT的base值，执行lgdt指令后，limit和base分别被加载到GDTR的limit和base域。

# 3. 初始化IDT

上面关于GDT的处理情况同样适用于IDT的处理，可以静态建立IDT后，再加载到IDTR里，然后像上面一样，在完成初始化之前，重定位在另一个受保护的位置再重新加载IDTR。

## 3.1 动态设置IDT

相比GDT，IDT更需要运行时的设置，有可能会对一个Interrupt Handler进行重新设置，或新增加一些中断处理程序。下面的函数用于设置IDT描述符。

```asm
set_interrupt_handler(int vector, void(*)()handler)
input:
    esi: vector, edi:handler

__set_interrupt_handler:
    sidt [__idt_pointer + 2]
    mov [eax + esi * 8 + 4], edi                            ; set offset[31:16]
    mov [eax + esi * 8], di                                 ; set offset[15:0]
    mov DWORD [eax + esi * 8 + 2], kernel_code32_sel        ; set selector
    mov WORD [eax + esi * 8 + 5], 80h | INTERRUPT_GATE32    ; P=1, DPL=0
    ret

set_user_interrupt_handler(int vector, void(*)()handler)
input:
    esi:vector, edi:handler

__set_user_interrupt_handler:
    sidt [__idt_pointer]
    mov eax, [__idt_pointer + 2]
    mov [eax + esi * 8 + 4], edi                            ; set offset[31:16]
    mov [eax + esi * 8], di                                 ; set offset[15:0]
    mov DWORD [eax + esi * 8 + 2], kernel_code32_sel        ; set selector
    mov WORD [eax + esi * 8 + 5], 0E0h | INTERRUPT_GATE32   ; P=1, DPL=3
    ret
```
这两个函数一个将Interrupt-gate的DPL设为0级，一个设为3级，可以开放给用户调用。在运行时修改gate描述符，也可以用于新增一个Interrupt-gate描述符。

## 3.2 加载IDTR

使用指令lidt加载IDTR。
```asm
    lidt [__idt_pointer]            ; load IDT to IDTR
```
原理和用法是与lgdt指令一样的。

# 4. 切换到保护模式

Intel推荐的步骤如下。
1. 关闭响应中断，包括Maskable Interrupt（可屏蔽的中断）和Non-Maskable Interrupt（不可屏蔽中断）。
2. 使用lgdt指令加载GDTR。
3. 使用mov cr0,reg32指令置CR0.PE控制位，切到保护模式。
4. 在mov指令后面，紧接着需要一条jmp或call指令，这里需要使用far jmp/call形式，提供一个同级权限的CS Selector来更新CS寄存器。
5. 如果需使用LDT，则执行lldt指令加载LDTR。
6. 使用ltr指令加载TR。
7. 更新必要的Data段寄存器，典型地需要更新SS寄存器和DS寄存器。
8. 使用lidt指令加载IDTR，建立IDT。
9. 打开中断响应，包括可屏蔽中断和不可屏蔽中断。

下面是在每个实验里的setup.asm模块里切换到保护模式的代码。
```asm
    bits 16
setup_entry:
    cli
    NMI_DISABLE
    
    db 0x66
    lgdt [__gdt_pointer]                ; load GDT
    
    db 0x66
    lidt [__idt_pointer]                ; load IDT
    
    ; set TSS Descriptor
    mov WORD [tss32_desc], 0x68 + IOBITMAP_END - IOBITMAP
    mov WORD [tss32_desc + 2], TSS32_SEG
    mov BYTE [tss32_desc + 5], 0x80 | TSS32
    
    mov eax, cr0
    bts eax, 0                          ; CR0.PE=1
    mov cr0, eax
    
    jmp kernel_code32_sel:entry32
    
    ; protected mode code
    bits 32
entry32:
    mov ax, kernel_data32_sel           ; set data segment
    mov ds, ax
    mov es, ax
    mov ss, ax
    mov esp, 0x7ff0
    
    ; load TSS segment
    mov ax, tss32_sel
    ltr ax
```
这段代码的前半部分运行在16位实模式下，后半部分运行在32位保护模式里，注意需要给编译器为不同的代码提供一个指示字bits 16和bits 32，那么运行在16位的编译为16位代码，后面的32位保护模式编译为32位代码。

setup.asm里在切换到保护模式前加载GDT和IDT，IDT在前在后并没有什么影响。而TSS段和LDT则必须在进入保护模式后才能加载。
```asm
    jmp kernel_code32_sel:entry32
```
这条指令时关键的地方，使用far jmp刷新CS寄存器，更新指令流，执行完这条指令处理器将进入32位保护模式。

**在未执行这条far jmp指令前，处理器属于什么状态？**

属于保护模式。一旦将CR0.PE置位，表明已经进入保护模式。不过它属于16位的保护模式，因为CS寄存器的状态时由16位的实模式遗留下来的。CS.D=0属于16位默认代码，CS.limit=0xFFFF还是64K段界限。只有far jmp执行后才刷新CS寄存器内部的Cache部分。

这个情景很容易让人联想到，当处理器初始化后，执行位于BIOS里的第一条far jmp指令前的状态和这个切换到保护模式未执行far jmp指令的状态很相像。所不同的是一个进入实模式，一个是进入保护模式。

进入保护模式后，紧接着对DS、SS、ES进行了更新，设置了ESP寄存器，这些都是一个正确的执行环境里不可缺少的。
接着加载了TR，建立TSS段。至此一个可运行的保护模式环境已经建立了。